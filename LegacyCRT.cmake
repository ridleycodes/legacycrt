FUNCTION(__LEGACY_CRT_GET_TARGET_ARCH TARGET_ARCH_FILE TARGET_ARCH)
    TRY_COMPILE(
        compile_result_unused
        "${CMAKE_BINARY_DIR}"
        "${TARGET_ARCH_FILE}"
        OUTPUT_VARIABLE COMPILER_OUTPUT
    )
    STRING(REGEX MATCH "cmake_ARCH ([a-zA-Z0-9_]+)" ARCH "${COMPILER_OUTPUT}")
    STRING(REPLACE "cmake_ARCH " "" ARCH "${ARCH}")
    SET(${TARGET_ARCH} "${ARCH}" PARENT_SCOPE)
ENDFUNCTION(__LEGACY_CRT_GET_TARGET_ARCH)

FUNCTION(__LEGACY_CRT_GET_FLAGS COMPILE_FLAGS LINK_FLAGS IS_DLL IS_CONSOLE APPEND_ENTRY_POINT)
    SET(_CF "-DUSE_LEGACY_CRT")
    SET(IS_DLL ${IS_DLL})
    SET(IS_CONSOLE ${IS_CONSOLE})
    SET(APPEND_ENTRY_POINT ${APPEND_ENTRY_POINT})
    IF(NOT TARGET_ARCH)
        SET(TARGET_ARCH "X86")
    ENDIF(NOT TARGET_ARCH)
    IF(IS_DLL)
        SET(_CF "${_CF} -D__LEGACY_CRT_CRT_IS_DLL")
    ENDIF(IS_DLL)
    IF(IS_CONSOLE)
        SET(_CF "${_CF} -D__LEGACY_CRT_CONSOLE_SUBSYSTEM")
    ELSE(IS_CONSOLE)
        SET(_CF "${_CF} -D__LEGACY_CRT_WINDOWS_SUBSYSTEM")
    ENDIF(IS_CONSOLE)
    IF(LEGACY_CRT_USE_OLD_CRT)
        SET(_CF "${_CF} -D__LEGACY_CRT_USE_OLD_CRT")
    ENDIF(LEGACY_CRT_USE_OLD_CRT)

    IF(MSVC)
        IF(IS_CONSOLE)
            SET(__S "CONSOLE")
        ELSE(IS_CONSOLE)
            SET(__S "WINDOWS")
        ENDIF(IS_CONSOLE)

        SET(_CF "${_CF} -D_NO_CRT_STDIO_INLINE -D_CRT_SECURE_NO_WARNINGS")
        SET(_LF "/NODEFAULTLIB")

        IF(NOT MSVC_VERSION LESS 1300)
            SET(_CF "${_CF} /GS-")
        ENDIF(NOT MSVC_VERSION LESS 1300)
        IF(NOT MSVC_VERSION LESS 1910)
            SET(_CF "${_CF} /JMC-")
        ENDIF(NOT MSVC_VERSION LESS 1910)
        IF(TARGET_ARCH STREQUAL "X86")
            SET(_CF "${_CF} -D_USE_32BIT_TIME_T")
        ENDIF(TARGET_ARCH STREQUAL "X86")

        IF(APPEND_ENTRY_POINT)
            IF(IS_DLL)
                SET(_LF "${_LF} /entry:DllMainCRTStartup")
            ELSE(IS_DLL)
                SET(_LF "${_LF} /entry:mainCRTStartup")
            ENDIF(IS_DLL)
        ENDIF(APPEND_ENTRY_POINT)
        IF(NOT MSVC_VERSION LESS 1100)
            SET(_LF "${_LF} /FIXED:NO")
        ENDIF(NOT MSVC_VERSION LESS 1100)
        IF(NOT MSVC_VERSION LESS 1400)
            SET(_LF "${_LF} /MANIFEST:NO")
        ENDIF(NOT MSVC_VERSION LESS 1400)

        IF((CMAKE_C_COMPILER_ID MATCHES "Clang" OR MSVC_VERSION LESS 1600) AND TARGET_ARCH STREQUAL "X86")
            SET(_LF "${_LF} /SUBSYSTEM:${__S},3.10 /OSVERSION:1.0 /VERSION:1.0")
        ELSE((CMAKE_C_COMPILER_ID MATCHES "Clang" OR MSVC_VERSION LESS 1600) AND TARGET_ARCH STREQUAL "X86")
            IF(TARGET_ARCH STREQUAL "X86")
                SET(_LF "${_LF} /SUBSYSTEM:${__S},5.01")
            ELSEIF(TARGET_ARCH STREQUAL "X64" OR TARGET_ARCH STREQUAL "IA64")
                SET(_LF "${_LF} /SUBSYSTEM:${__S},5.02")
            ELSEIF(TARGET_ARCH STREQUAL "ARM" OR TARGET_ARCH STREQUAL "ARM64")
                SET(_LF "${_LF} /SUBSYSTEM:${__S},6.02")
            ENDIF(TARGET_ARCH STREQUAL "X86")
        ENDIF((CMAKE_C_COMPILER_ID MATCHES "Clang" OR MSVC_VERSION LESS 1600) AND TARGET_ARCH STREQUAL "X86")
    ELSE(MSVC)
        IF(IS_CONSOLE)
            SET(__S "console")
        ELSE(IS_CONSOLE)
            SET(__S "windows")
        ENDIF(IS_CONSOLE)
        
        SET(_CF "${_CF} -D__CRTDLL__ -mfpmath=387 -fno-stack-protector -fno-ident")
        SET(_LF "-nostdlib -pie")
        IF(TARGET_ARCH STREQUAL "X86")
            SET(_CF "${_CF} -D_USE_32BIT_TIME_T")
        ENDIF(TARGET_ARCH STREQUAL "X86")

        IF(APPEND_ENTRY_POINT)
            IF(TARGET_ARCH STREQUAL "X86")
                IF(IS_DLL)
                    SET(_LF "${_LF} -Wl,-e,_DllMainCRTStartup@12")
                ELSE(IS_DLL)
                    SET(_LF "${_LF} -Wl,-e,_mainCRTStartup")
                ENDIF(IS_DLL)
            ELSE(TARGET_ARCH STREQUAL "X86")
                IF(IS_DLL)
                    SET(_LF "${_LF} -Wl,-e,DllMainCRTStartup")
                ELSE(IS_DLL)
                    SET(_LF "${_LF} -Wl,-e,mainCRTStartup")
                ENDIF(IS_DLL)
            ENDIF(TARGET_ARCH STREQUAL "X86")
        ENDIF(APPEND_ENTRY_POINT)

        IF(TARGET_ARCH STREQUAL "X86")
            SET(_LF "${_LF} -Wl,--subsystem,${__S}:3.10 -Wl,--major-os-version,1 -Wl,--minor-os-version,0")
        ELSEIF(TARGET_ARCH STREQUAL "X64" OR TARGET_ARCH STREQUAL "IA64")
            SET(_LF "${_LF} -Wl,--subsystem,${__S}:5.02")
        ELSEIF(TARGET_ARCH STREQUAL "ARM" OR TARGET_ARCH STREQUAL "ARM64")
            SET(_LF "${_LF} -Wl,--subsystem,${__S}:6.02")
        ENDIF(TARGET_ARCH STREQUAL "X86")
    ENDIF(MSVC)

    SET(${COMPILE_FLAGS} "${_CF}" PARENT_SCOPE)
    SET(${LINK_FLAGS} "${_LF}" PARENT_SCOPE)
ENDFUNCTION(__LEGACY_CRT_GET_FLAGS COMPILE_FLAGS LINK_FLAGS IS_DLL IS_CONSOLE APPEND_ENTRY_POINT)

FUNCTION(__LEGACY_CRT_ADD_CRT_LIBRARY CRT_SRC_LOCATION)
    ### Generate CRT lib
    IF(LEGACY_CRT_USE_OLD_CRT)
        SET(CRT_DEF "${CRT_SRC_LOCATION}/crtdll.def")
        SET(CRT_BASENAME "crtdll")
    ELSE(LEGACY_CRT_USE_OLD_CRT)
        SET(CRT_DEF "${CRT_SRC_LOCATION}/msvcrt.def")
        SET(CRT_BASENAME "msvcrt")
    ENDIF(LEGACY_CRT_USE_OLD_CRT)

    IF(MSVC)
        SET(LIB_EXT ".lib")
        SET(CRT_LIB "${CMAKE_CURRENT_BINARY_DIR}/${CRT_BASENAME}${LIB_EXT}")
        SET(LIB_CMD ${CMAKE_LIB_COMPILER} /nologo /def:"${CRT_DEF}" /out:"${CRT_LIB}" /machine:"${TARGET_ARCH}")
    ELSE(MSVC)
        STRING(TOLOWER "${TARGET_ARCH}" GNU_MACHINE_ARCH)
        IF(TARGET_ARCH STREQUAL "X86")
            SET(GNU_MACHINE_ARCH "i386")
        ELSEIF(TARGET_ARCH STREQUAL "X64")
            SET(GNU_MACHINE_ARCH "i386:x86-64")
        ELSEIF(TARGET_ARCH STREQUAL "ARM64")
            SET(GNU_MACHINE_ARCH "aarch64")
        ENDIF(TARGET_ARCH STREQUAL "X86")
        SET(LIB_EXT ".a")
        SET(CRT_LIB "${CMAKE_CURRENT_BINARY_DIR}/lib${CRT_BASENAME}${LIB_EXT}")
        SET(LIB_CMD ${CMAKE_LIB_COMPILER} -d "${CRT_DEF}" -l "${CRT_LIB}" -m "${GNU_MACHINE_ARCH}")
    ENDIF(MSVC)

    ADD_CUSTOM_COMMAND(
        OUTPUT "${CRT_LIB}"
        COMMAND ${LIB_CMD}
        DEPENDS "${CRT_DEF}"
    )
    
    ADD_CUSTOM_TARGET(legacy_crt_library DEPENDS "${CRT_LIB}")
    SET_TARGET_PROPERTIES(legacy_crt_library PROPERTIES
        CRT_LOC            "${CRT_SRC_LOCATION}"
        CRT_LIB             "${CRT_LIB}")
ENDFUNCTION(__LEGACY_CRT_ADD_CRT_LIBRARY)

MACRO(ADD_LEGACY_CRT CRT_LOCATION)
    IF(NOT DEFINED __LEGACY_CRT_INITIALIZED)
        IF(NOT WIN32)
            MESSAGE(FATAL_ERROR "This program is for Windows targets only. Detected: ${CMAKE_SYSTEM_NAME}")
        ENDIF(NOT WIN32)
        IF(NOT CMAKE_CROSSCOMPILING)
            IF(MSVC)
                SET(CMAKE_LIB_COMPILER "lib.exe")
            ELSE(MSVC)
                SET(CMAKE_LIB_COMPILER "dlltool")
            ENDIF(MSVC)
        ENDIF(NOT CMAKE_CROSSCOMPILING)

        OPTION(LEGACY_CRT_USE_OLD_CRT "Link to CRTDLL.DLL instead of MSVCRT.DLL (X86 only)" ON)

        ### Detect Architecture
        IF(NOT CMAKE_CROSSCOMPILING AND NOT DEFINED TARGET_ARCH)
            __LEGACY_CRT_GET_TARGET_ARCH("${CRT_LOCATION}/cmake/arch.c" TARGET_ARCH)
        ENDIF(NOT CMAKE_CROSSCOMPILING AND NOT DEFINED TARGET_ARCH)
        IF(NOT TARGET_ARCH STREQUAL "X86")
            SET(LEGACY_CRT_USE_OLD_CRT OFF)
        ENDIF(NOT TARGET_ARCH STREQUAL "X86")

        IF(MSVC)
            SET(CMAKE_MSVC_RUNTIME_CHECKS "")
            ### Disable default CMake flags that are not compatible with the CRT
            # Unfortunately, this has to affect the entire project to work
            FOREACH(FLAG_VAR
                CMAKE_C_FLAGS CMAKE_C_FLAGS_DEBUG CMAKE_C_FLAGS_RELEASE
                CMAKE_C_FLAGS_MINSIZEREL CMAKE_C_FLAGS_RELWITHDEBINFO)
                STRING(REGEX REPLACE "/RTC(su|[1su])" "" ${FLAG_VAR} "${${FLAG_VAR}}")  # Disable runtime error checks
                STRING(REPLACE "/GZ" "" ${FLAG_VAR} "${${FLAG_VAR}}")                   # Disable stack frame runtime
                SET(${FLAG_VAR} "${${FLAG_VAR}}" CACHE STRING "" FORCE)                 # Affect entire project
            ENDFOREACH(FLAG_VAR)
        ENDIF(MSVC)

        ### Add CRT files
        __LEGACY_CRT_ADD_CRT_LIBRARY(${CRT_LOCATION}/src)

        ### Add patcher_pe in case it is needed
        IF(MSVC AND NOT (CMAKE_C_COMPILER_ID MATCHES "Clang" OR MSVC_VERSION LESS 1600) AND TARGET_ARCH STREQUAL "X86")
            ADD_EXECUTABLE(__legacy_crt_patch_pe "${CRT_LOCATION}/cmake/patch_pe.c")
            SET_TARGET_PROPERTIES(__legacy_crt_patch_pe PROPERTIES
                COMPILE_DEFINITIONS "_CRT_SECURE_NO_WARNINGS")
        ENDIF(MSVC AND NOT (CMAKE_C_COMPILER_ID MATCHES "Clang" OR MSVC_VERSION LESS 1600) AND TARGET_ARCH STREQUAL "X86")
        SET(__LEGACY_CRT_INITIALIZED ON)
    ENDIF(NOT DEFINED __LEGACY_CRT_INITIALIZED)
ENDMACRO(ADD_LEGACY_CRT)

FUNCTION(ADD_LEGACY_CRT_EXECUTABLE EXECUTABLE_NAME IS_CONSOLE ...)
    SET(_SRC ${ARGV})
    LIST(REMOVE_AT _SRC 0 1)
    GET_TARGET_PROPERTY(_CRT legacy_crt_library CRT_LOC)
    IF(MSVC)
        ADD_EXECUTABLE(${EXECUTABLE_NAME} WIN32 "${_CRT}/crt.c" 
                       "${_CRT}/lldiv.obj" "${_CRT}/lldvrm.obj" "${_CRT}/llmul.obj" "${_CRT}/llrem.obj" "${_CRT}/llshl.obj" "${_CRT}/llshr.obj"  
                       ${_SRC})
    ELSE(MSVC)
        ADD_EXECUTABLE(${EXECUTABLE_NAME} WIN32 "${_CRT}/crt.c" "${_CRT}/gccmath.c" ${_SRC})
    ENDIF(MSVC)
    __LEGACY_CRT_GET_FLAGS(_CF _LF OFF ${IS_CONSOLE} ON)
    SET_TARGET_PROPERTIES(${EXECUTABLE_NAME} PROPERTIES
        COMPILE_FLAGS "${_CF}"
        LINK_FLAGS "${_LF}"
        MSVC_RUNTIME_CHECKS ""
    )
    ADD_DEPENDENCIES(${EXECUTABLE_NAME} legacy_crt_library)
    GET_TARGET_PROPERTY(CRT_LIB legacy_crt_library CRT_LIB)
    TARGET_LINK_LIBRARIES(${EXECUTABLE_NAME} "${CRT_LIB}")
    IF(NOT IS_CONSOLE)
        TARGET_LINK_LIBRARIES(${EXECUTABLE_NAME} kernel32)
    ENDIF(NOT IS_CONSOLE)

    IF(MSVC AND NOT (CMAKE_C_COMPILER_ID MATCHES "Clang" OR MSVC_VERSION LESS 1600) AND TARGET_ARCH STREQUAL "X86")
        IF(${CMAKE_VERSION} VERSION_LESS "3.0")
            GET_TARGET_PROPERTY(EXECUTABLE_LOCATION ${EXECUTABLE_NAME} LOCATION)
            GET_TARGET_PROPERTY(PATCHER_LOCATION __legacy_crt_patch_pe LOCATION)
        ELSE(${CMAKE_VERSION} VERSION_LESS "3.0")
            SET(EXECUTABLE_LOCATION "$<TARGET_FILE:${EXECUTABLE_NAME}>")
            SET(PATCHER_LOCATION "$<TARGET_FILE:__legacy_crt_patch_pe>")
        ENDIF(${CMAKE_VERSION} VERSION_LESS "3.0")

        ADD_CUSTOM_COMMAND(
            TARGET ${EXECUTABLE_NAME} POST_BUILD
            COMMAND "${PATCHER_LOCATION}" "${EXECUTABLE_LOCATION}"
        )
        ADD_DEPENDENCIES(${EXECUTABLE_NAME} __legacy_crt_patch_pe)
    ENDIF(MSVC AND NOT (CMAKE_C_COMPILER_ID MATCHES "Clang" OR MSVC_VERSION LESS 1600) AND TARGET_ARCH STREQUAL "X86")
ENDFUNCTION(ADD_LEGACY_CRT_EXECUTABLE)

FUNCTION(ADD_LEGACY_CRT_DLL DLL_NAME IS_CONSOLE ...)
    SET(_SRC ${ARGV})
    LIST(REMOVE_AT _SRC 0 1)
    GET_TARGET_PROPERTY(_CRT legacy_crt_library CRT_LOC)
    IF(MSVC)
        ADD_LIBRARY(${DLL_NAME} SHARED "${_CRT}/crt.c" 
                    "${_CRT}/lldiv.obj" "${_CRT}/lldvrm.obj" "${_CRT}/llmul.obj" "${_CRT}/llrem.obj" "${_CRT}/llshl.obj" "${_CRT}/llshr.obj" 
                    ${_SRC})
    ELSE(MSVC)
        ADD_LIBRARY(${DLL_NAME} SHARED "${_CRT}/crt.c" "${_CRT}/gccmath.c" ${_SRC})
    ENDIF(MSVC)
    __LEGACY_CRT_GET_FLAGS(_CF _LF ON ${IS_CONSOLE} ON)
    SET_TARGET_PROPERTIES(${DLL_NAME} PROPERTIES
        COMPILE_FLAGS "${_CF}"
        LINK_FLAGS "${_LF}"
        MSVC_RUNTIME_CHECKS ""
    )

    ADD_DEPENDENCIES(${DLL_NAME} legacy_crt_library)
    GET_TARGET_PROPERTY(CRT_LIB legacy_crt_library CRT_LIB)
    TARGET_LINK_LIBRARIES(${DLL_NAME} "${CRT_LIB}")
ENDFUNCTION(ADD_LEGACY_CRT_DLL)

FUNCTION(ADD_LEGACY_CRT_LIBRARY LIBRARY_NAME IS_CONSOLE ...)
    SET(_SRC ${ARGV})
    LIST(REMOVE_AT _SRC 0 1)
    GET_TARGET_PROPERTY(_CRT legacy_crt_library CRT_LOC)
    IF(MSVC)
        ADD_LIBRARY(${LIBRARY_NAME} STATIC 
                    "${_CRT}/lldiv.obj" "${_CRT}/lldvrm.obj" "${_CRT}/llmul.obj" "${_CRT}/llrem.obj" "${_CRT}/llshl.obj" "${_CRT}/llshr.obj" 
                    ${_SRC})
    ELSE(MSVC)
        ADD_LIBRARY(${LIBRARY_NAME} STATIC "${_CRT}/gccmath.c" ${_SRC})
    ENDIF(MSVC)
    __LEGACY_CRT_GET_FLAGS(_CF _LF OFF ${IS_CONSOLE} OFF)
    SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES
        COMPILE_FLAGS "${_CF}"
        MSVC_RUNTIME_CHECKS ""
    )

    ADD_DEPENDENCIES(${LIBRARY_NAME} legacy_crt_library)
    GET_TARGET_PROPERTY(CRT_LIB legacy_crt_library CRT_LIB)
    TARGET_LINK_LIBRARIES(${LIBRARY_NAME} "${CRT_LIB}")
ENDFUNCTION(ADD_LEGACY_CRT_LIBRARY)
